# Before version 0.70
# Maintainer: eltonff <eltonfabricio10@gmail.com>

# After 0.70 change to fork of https://aur.archlinux.org/cgit/aur.git/tree/PKGBUILD?h=python-webpy by Contributor: Brenden Davidson <davidson.brenden15@gmail.com>
# And modified by Bruno Goncalves <www.biglinux.com.br> to turn compatible with python 3.11 and 3.12 in one package

_module="webpy"
_pkgname="python-$_module"
pkgname="bbv-$_module"
pkgver=0.70
pkgrel=1
replaces=('python-webpy')
provides=('python-webpy')
pkgdesc="A web framework for Python"
arch=("any")
#url="https://webpy.org/"
url="https://github.com/webpy/webpy"
license=('Public Domain')

depends=(
  'python'
  'python-cheroot'
  'python-multipart'
)
makedepends=(
  'python-build'
  'python-installer'
  'python-setuptools'
  'python-wheel'
)

#_tag="${pkgver%%.r*}"
_tag="$_module-${pkgver%%.r*}"
_pkgsrc="$_module-$_tag"
_pkgext="tar.gz"
source=(
  "$_module-${pkgver%%.r*}.$_pkgext"::"$url/archive/refs/tags/$_tag.$_pkgext"
)
sha256sums=(
  "eee36e423b5e85463145159d94735e3c3a4c1c8078555042d8139348350b0022"
)

build() {
  cd "$_pkgsrc"
  python -m build --no-isolation --wheel
}

package() {
  cd "$_pkgsrc"
  python -m installer --destdir="${pkgdir:?}" dist/*.whl
  install -Dm644 "LICENSE.txt" "${pkgdir:?}/usr/share/licenses/${_pkgname:?}/LICENSE"

  # turn compatible with python 3.11 and 3.12
  cd ${pkgdir:?}
  if [[ -e usr/lib/python3.12 ]]; then
    mkdir -p usr/lib/python3.11/site-packages
    cd usr/lib/python3.11/site-packages
    ln -s ../../python3.12/site-packages/web web
  elif  [[ -e usr/lib/python3.11 ]]; then
    mkdir -p usr/lib/python3.12/site-packages
    cd usr/lib/python3.12/site-packages
    ln -s ../../python3.11/site-packages/web web
  fi

}
